1. <program> ===> <otherFunctions> <mainFunction>
2. <mainFunction>===> TK_MAIN <stmts> TK_END
3. <otherFunctions>===> <function><otherFunctions>
4. <otherFunctions>===> eps
5. <function>===> TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
6. <input_par>===> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
7. <output_par>===> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
8. <output_par>===> eps
9. <parameter_list>===> <dataType> TK_ID <remaining_list>
10. <dataType>===> <arrayDatatype> <primitiveDatatype>
11. <dataType>===> <constructedDatatype>
12. <primitiveDatatype>===> TK_INT
13. <primitiveDatatype>===> TK_REAL
14. <constructedDatatype>===> TK_RECORD TK_RECORDID
15. <remaining_list>===> TK_COMMA <parameter_list>
16. <remaining_list>===> eps
17. <stmts>===> <typeDefinitions> <declarations> <otherStmts> <returnStmt>
18. <typeDefinitions>===> <typeDefinition> <typeDefinitions>
19. <typeDefinitions>===> eps
20. <typeDefinition>===> TK_RECORD TK_RECORDID <fieldDefinitions> TK_ENDRECORD TK_SEM
21. <fieldDefinitions>===> <fieldDefinition> <fieldDefinition> <moreFields>
22. <fieldDefinition>===> TK_TYPE <primitiveDatatype> TK_COLON TK_FIELDID TK_SEM
23. <moreFields>===> <fieldDefinition> <moreFields>
24. <moreFields>===> eps
25. <declarations> ===> <declaration> <declarations>
26. <declarations> ===> eps
27. <declaration>===> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
28. <global_or_not>===>TK_COLON TK_GLOBAL
29. <global_or_not>===> eps
30. <otherStmts>===> <stmt> <otherStmts>
31. <otherStmts>===> eps
32. <stmt>===> <assignmentStmt>
33. <stmt>===> <iterativeStmt>
34. <stmt>===> <conditionalStmt>
35. <stmt>===> <ioStmt>
36. <stmt>===> <funCallStmt>
37. <assignmentStmt>===> <singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
38. <singleOrRecId>===> TK_ID <new_24>
39. <new_24>===> TK_DOT TK_FIELDID
40. <new_24>===> TK_SQL TK_NUM TK_SQR
41. <new_24>===> eps
42. <funCallStmt>===> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
43. <outputParameters> ==> TK_SQL <idList> TK_SQR TK_ASSIGNOP
44. <outputParameters> ==> eps
45. <inputParameters>===> TK_SQL <idList> TK_SQR
46. <iterativeStmt>===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt> <otherStmts> TK_ENDWHILE
47. <conditionalStmt>===> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt> <otherStmts> <elsePart>
48. <elsePart>===> TK_ELSE <stmt> <otherStmts> TK_ENDIF
49. <elsePart>===> TK_ENDIF
50. <ioStmt>===> TK_READ TK_OP <singleOrRecId> TK_CL TK_SEM
51. <ioStmt>===> TK_WRITE TK_OP <all> TK_CL TK_SEM
52. <arithmeticExpression> ===> <term> <expPrime>
53. <expPrime> ===> <lowPrecedenceOperators> <term> <expPrime>
54. <expPrime> ===> eps
55. <term>===> <factor> <termPrime>
56. <termPrime> ===> <highPrecedenceOperators> <factor> <termPrime>
57. <termPrime> ===> eps
58. <factor> ===> TK_OP <arithmeticExpression> TK_CL
59. <factor> ===> <all>
60. <highPrecedenceOperators>===> TK_MUL
61. <highPrecedenceOperators>===> TK_DIV
62. <lowPrecedenceOperators> ===> TK_PLUS
63. <lowPrecedenceOperators> ===> TK_MINUS
64. <all>===> TK_NUM
65. <all>===> TK_RNUM
66. <all>===> <singleOrRecId>
67. <booleanExpression>===>TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL
68. <booleanExpression>===> <all> <relationalOp> <all>
69. <booleanExpression>===> TK_NOT TK_OP <booleanExpression> TK_CL
70. <logicalOp>===>TK_AND
71. <logicalOp>===> TK_OR
72. <relationalOp>===> TK_LT
73. <relationalOp>===> TK_LE
74. <relationalOp>===> TK_EQ
75. <relationalOp>===> TK_GT
76. <relationalOp>===> TK_GE
77. <relationalOp>===> TK_NE
78. <returnStmt>===> TK_RETURN <optionalReturn> TK_SEM
79. <optionalReturn>===> TK_SQL <idList> TK_SQR
80. <optionalReturn>===> eps
81. <idList>===> TK_ID <more_ids>
82. <more_ids>===> TK_COMMA <idList>
83. <more_ids>===> eps
84. <arrayDatatype>===> TK_ARRAY TK_SQL TK_NUM TK_DOT TK_DOT TK_NUM TK_SQR TK_OF
85. <arrayDatatype>===> eps
